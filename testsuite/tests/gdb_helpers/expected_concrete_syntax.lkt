import lexer_example

@with_lexer(foo_lexer)
grammar foo_grammar {
    @main_rule main_rule <- list+(or(example | parens))
    node_list <- list*(or(example | parens))
    parens <- Parens("(" node_list ")")
    example <- Example(@Example)
}

@abstract
class FooNode implements Node[FooNode] {
    @exported
    fun id_unit(u: AnalysisUnit): AnalysisUnit = u

    @exported
    fun id_node(n: FooNode): FooNode = n

    @exported
    fun test_strings(): Int = {
        val empty = "";
        val foo = "foo";
        val s_quote = "'";
        val d_quote = "\"";
        val quote_mix = "\"'";
        val lf = "\n";
        val nul = "\u0000";
        val arr = [empty, foo, s_quote, d_quote, quote_mix, lf, nul];

        arr.length()
    }

    @exported
    fun test_symbols(): Int = {
        val empty = null[Symbol];
        val foo = s"foo";
        val arr = [empty, foo];

        arr.length()
    }

    fun get_rebindings(inverse: Bool): EnvRebindings = {
        val example_nodes = node.parent.as[ASTList[FooNode]].as_array();
        val n1 = example_nodes?[if inverse then 1 else 2];
        val n2 = example_nodes?[if inverse then 2 else 1];

        null[EnvRebindings].append_rebinding(n1.children_env(), n2.children_env())
    }

    @exported
    fun test_rebindings(): Int = {
        val null_var = null[EnvRebindings];
        val r1 = node.get_rebindings(false);
        val r2 = node.get_rebindings(true);
        val concat = r1.concat_rebindings(r2);
        val arr = [null_var, r1, r2, concat];

        arr.length()
    }

    @exported
    fun test_envs(): Int = {
        val null_var = null[LexicalEnv];
        val primary = node.children_env();
        val orphan = primary.env_orphan();
        val group = [primary, null_var].env_group();
        val rebound = primary.rebind_env(node.get_rebindings(false));
        val arr = [null_var, primary, orphan, group, rebound];

        arr.length()
    }

    @exported
    fun test_entities(): Int = {
        val null_root = null[Entity[FooNode]];
        val null_example = null[Entity[Example]];
        val ent_root = self;
        # BREAK:test_state
        val ent_example = ent_root.as[Example];
        val rebound = Entity[FooNode](
            node=node, info=EntityInfo(
                md=null[Metadata], rebindings=node.get_rebindings(false), from_rebound=false
            )
        );
        val arr = [null_root, null_example.as[FooNode], ent_root, ent_example.as[FooNode], rebound];

        arr.length()
    }

    @exported
    fun test_arrays(): Int = {
        val empty = null[Array[FooNode]];
        val single = [node];
        val complete = node.parent.as[ASTList[FooNode]].as_array();
        val arr = empty & single & complete;

        arr.length()
    }

    @exported
    fun test_vectors(): Int = {
        val arr = node.parents().map((n) => n.children);
        # BREAK:test_vectors

        arr.length()
    }

    @exported
    fun test_tokens(): Int = {
        val null_var = null[Token];
        val first = node.token_start();
        val arr = [null_var, first];

        arr.length()
    }

    fun control_flow_helper(item: Array[FooNode]): Int = item.length() + 1

    @exported
    fun test_control_flow(i: Int): Int = {
        val nodes = node.parent.children;
        val arr = nodes.map(
            (n) => n.parents().length() + {
                val item = n.children;

                node.control_flow_helper(item)
            }
        );

        i + arr.length()
    }

    fun int_array_sum(ints: Array[Int], i: Int): Int =
        if i <= ints.length() then ints?[i] + node.int_array_sum(ints, i + 1) else 0

    @exported
    fun test_recursive_cf(): Int = {
        val self_count = if node is Example then 1 else 0;
        val children_counts = node.children.map((n) => n.test_recursive_cf());
        # BREAK:recursive_inner_loop

        node.int_array_sum(children_counts, 0) + self_count
    }

    @exported
    fun test_struct(i: Int): Int = {
        val result = MyStruct(a=i, b=i + 10);

        result.a
    }
}

class Example: FooNode implements TokenNode {
    env_spec {
        add_env()
    }
}

class Parens: FooNode {
    @parse_field items: ASTList[FooNode]
}

struct MyStruct {
    a: Int
    b: Int
}
