# Check that the Unparsing/Prettier API works as expect

from lexer_example import foo_lexer

@with_lexer(foo_lexer)
@with_unparsers
grammar foo_grammar {
    @main_rule main_rule <- list*(or(annot_var_decl | annot_fun_decl | block))
    name <- Name(@Identifier)
    type_ref <- name
    annotations <- AnnotationList*(name, ",")
    null_qual <- NullQual("null")
    var_decl <- VarDecl(
        null_qual "var" name ":" type_ref "=" expr ";"
    )
    annot_var_decl <- AnnotatedDecl(annotations var_decl)
    param_spec <- ParamSpec(name ":" type_ref ?pick("=" expr))
    fun_decl <- FunDecl(
        "def"
        name
        pick("("
        list*(
            or(param_spec | skip(ErrorParamSpec)), ",", allow_trailing
        ).dont_skip(")")
        ")") ":" type_ref null_qual "{" stmts "}"
    )
    stmts <- or(list+(labeled_stmts) | Stmts(list*(stmt)))
    annot_fun_decl <- AnnotatedDecl(annotations fun_decl)
    labeled_stmts <- LabeledStmts(name ":" list*(stmt))
    stmt <- or(
        | ExprStmt(expr ";")
        | AssignStmt(
            list+(name, ",", allow_leading) "=" expr ";"
        )
    )
    expr <- or(
        | Addition(expr "+" expr_1)
        | MinusExpr("-" expr_1 ?("(" list+(name, ",", allow_leading) ")"))
        | expr_1
    )
    expr_1 <- or(
        | DottedName(expr_1 "." name)
        | call
        | atom
    )
    atom <- or(number | ref)
    number <- Number(@Number)
    ref <- Ref(name)
    call_arg <- CallArg(?pick(name "=") expr)
    call <- or(
        | Call(name "(" list*(call_arg, ",") ")")
        | ExampleCall(
            "example" null(Name) "(" list*(call_arg, ",") ")"
        )
    )
    block <- or(
        | Block("{" list*(or(var_decl | fun_decl)) "}")
        | PlusBlock(
            "+" "{" PlusDecls*(or(var_decl | fun_decl)) "}"
        )
    )
}

@abstract
class FooNode implements Node[FooNode] {
    @exported
    fun table_join_needed(): Bool = {
        val this_decl = node.as[AnnotatedDecl]?.decl.as[VarDecl];
        val prev_decl =
            node
            .as_bare_entity
            .previous_sibling.
            as[AnnotatedDecl]
            ?.decl
            .as[VarDecl];

        not prev_decl.is_null
        and not this_decl.is_null
        and (
            if node.as[AnnotatedDecl].annotation_names.empty()
            then true
            else raise[Bool] PropertyError("predicate bug")
        )
        and prev_decl.name.text == "left"
        and this_decl.name.text == "right"
    }

    @exported
    fun table_join_bad_args(i: Int): Bool = i == 0

    @exported
    fun table_join_bad_rtype(): Int = 0

    @exported
    @memoized
    fun get_synth_node(): SynthNode = SynthNode()
}

@synthetic
class SynthNode: FooNode {
}

class AnnotatedDecl: FooNode {
    @parse_field annotation_names: AnnotationList
    @parse_field decl: Decl
}

@abstract
class BaseBlock: FooNode {
}

class Block: BaseBlock {
    @parse_field items: ASTList[Decl]
}

class PlusBlock: BaseBlock {
    @parse_field items: PlusDecls
}

class CallArg: FooNode {
    @parse_field @nullable name: Name
    @parse_field expr: Expr
}

@abstract
class Decl: FooNode {
    @abstract @parse_field name: Name
}

class FunDecl: Decl {
    @parse_field name: Name
    @parse_field args: ASTList[BaseParamSpec]
    @parse_field return_type: Name
    @parse_field is_null: NullQual
    @parse_field body: FooNode
}

class VarDecl: Decl {
    @parse_field is_null: NullQual
    @parse_field name: Name
    @parse_field type_expr: Name
    @parse_field value: Expr
}

@abstract
class Expr: FooNode {
}

class Addition: Expr {
    @parse_field lhs: Expr
    @parse_field rhs: Expr
}

class MinusExpr: Expr {
    @parse_field prefix: Expr
    @parse_field args: ASTList[Name]
}

class Call: Expr {
    @parse_field @nullable callee: Name
    @parse_field args: ASTList[CallArg]
}

class ExampleCall: Call {
}

class DottedName: Expr {
    @parse_field prefix: Expr
    @parse_field suffix: Name
}

class Number: Expr implements TokenNode {
}

class Ref: Expr {
    @parse_field name: Name
}

class PlusDecls: ASTList[Decl] {
}

class AnnotationList: ASTList[Name] {
}

class LabeledStmts: FooNode {
    @parse_field label: Name
    @parse_field stmts: ASTList[Stmt]
}

class Name: FooNode implements TokenNode {
}

@qualifier
enum class NullQual: FooNode {
}

@abstract
class BaseParamSpec: FooNode {
}

class ParamSpec: BaseParamSpec {
    @parse_field name: Name
    @parse_field type_expr: Name
    @parse_field @nullable default_expr: Expr
}

class ErrorParamSpec: BaseParamSpec implements ErrorNode {
}

@abstract
class Stmt: FooNode {
}

class AssignStmt: Stmt {
    @parse_field names: ASTList[Name]
    @parse_field expr: Expr
}

class ExprStmt: Stmt {
    @parse_field expr: Expr
}

class Stmts: FooNode {
    @parse_field stmts: ASTList[Stmt]
}
