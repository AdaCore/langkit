# Test that add_to_env actions that try to insert foreign nodes (as mapping
# value or metadata field) are properly rejected.

from lexer_example import foo_lexer

@with_lexer(foo_lexer)
grammar foo_grammar {
    @main_rule main_rule <- list+(or(scope | self_decl | foreign_decl))
    scope <- Scope(simple_identifier "{" list*(scope) "}")
    identifier <- or(
        | ScopedId(identifier "." simple_identifier)
        | simple_identifier
    )
    simple_identifier <- SimpleId(@Identifier)
    foreign_decl <- ForeignDecl(Synth(".") identifier)
    self_decl <- SelfDecl(
        "+" identifier ?pick("(" identifier ")")
    )
}

@abstract
class FooNode implements Node[FooNode] {
}

@synthetic
class DummySyntheticNode: FooNode {
}

class ForeignDecl: FooNode {
    @parse_field create_synthetic: Synth
    @parse_field id: Id

    @memoized
    fun node_for_env(): FooNode =
        if node.create_synthetic.as_bool() then DummySyntheticNode() else node

    env_spec {
        add_to_env_kv(
            node.id.simple_name().symbol, node.node_for_env(),
            dest_env=DesignatedEnv(
                kind=DesignatedEnvKind.direct_env,
                env_name=null[Symbol],
                direct_env=match node.id {
                    case simple: SimpleId =>
                        simple.node_env
                    case scoped: ScopedId =>
                        scoped.resolve(node.node_env).children_env
                }
            )
        )
    }
}

@abstract
class Id: FooNode {
    @abstract
    fun simple_name(): SimpleId

    @abstract
    fun resolve(base_env: LexicalEnv): Scope
}

class ScopedId: Id {
    @parse_field scope: Id
    @parse_field name: SimpleId

    fun simple_name(): SimpleId = node.name

    fun resolve(base_env: LexicalEnv): Scope =
        node
        .scope
        .resolve(base_env)
        .children_env
        .get_first(node.name.symbol)
        .node
        .as[Scope]
}

class SimpleId: Id implements TokenNode {
    fun simple_name(): SimpleId = node

    fun resolve(base_env: LexicalEnv): Scope =
        base_env.get_first(node.symbol).node.as[Scope]
}

class Scope: FooNode {
    @parse_field name: SimpleId
    @parse_field content: ASTList[Scope]

    env_spec {
        add_to_env_kv(node.name.symbol, node)
        add_env()
    }
}

class SelfDecl: FooNode {
    @parse_field id: Id
    @parse_field @nullable md_node: Id

    env_spec {
        add_to_env_kv(
            node.id.simple_name().symbol,
            node.id.resolve(node.node_env),
            metadata=Metadata(
                node=node.md_node.do((n) => n.resolve(node.node_env))
            )
        )
    }
}

@qualifier
enum class Synth: FooNode {
}

@metadata
struct Metadata {
    @used_in_equality node: FooNode
}
