# Test that Prediate works well with default argument values

from lexer_example import foo_lexer

@with_lexer(foo_lexer)
grammar foo_grammar {
    @main_rule main_rule <- Example("example")
}

dynvar my_var: Int

@abstract
class FooNode implements Node[FooNode] {
}

class Example: FooNode {
    var1: LogicVar
    var2: LogicVar

    # Simple case: the predicate only has two arguments with default values

    fun predicate1(
        n: Entity[FooNode],
        flag1: Bool = false,
        flag2: Bool = true
    ): Bool =
        flag1 and n.is_null and flag2

    @exported
    fun prop1(): Bool = {
        val eq = Example.predicate1%(node.var1, node.var2);

        eq.solve()
    }

    # More complex case: the predicate has one argument with a default value,
    # but also takes a dynamic variable.

    @with_dynvars(my_var)
    fun predicate2(n: Entity[FooNode], flag: Bool = false): Bool =
        n.is_null and my_var == 0 and flag

    @exported
    fun prop2(): Bool = {
        val eq = {
            bind my_var = 1;

            Example.predicate2%(node.var1, node.var2)
        };

        eq.solve()
    }

    # Variant of the complex case: the predicate takes both one argument with a
    # default value and a dynamic variable with a default value.

    @with_dynvars(my_var=0)
    fun predicate3(n: Entity[FooNode], flag: Bool = false): Bool =
        n.is_null and my_var == 0 and flag

    @exported
    fun prop3(): Bool = {
        val eq = {
            bind my_var = 1;

            Example.predicate3%(node.var1, node.var2)
        };

        eq.solve()
    }
}
