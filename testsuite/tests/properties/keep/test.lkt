# Check that the ".keep" property construct works as expected

from lexer_example import foo_lexer

@with_lexer(foo_lexer)
grammar foo_grammar {
    @main_rule main_rule <- list*(sequence)
    sequence <- Sequence("(" list*(sequence_item) ")")
    sequence_item <- or(Number(@Number) | Identifier(@Identifier))
}

@abstract
class FooNode implements Node[FooNode] {
    @exported
    fun not_nc_keep(items: Entity[ASTList[FooNode]]): Bool =
        # If "items" is null, .keep[] will return an empty array and so the
        # .all quantifier will return True.
        items.keep[Identifier].all((id) => id.is_null)

    @exported
    fun nc_keep(items: Entity[ASTList[FooNode]]): Bool =
        # If "items" is null, both .keep operation and the .all quantifier will
        # be short-circuited, so the property will return False.
        items?.keep[Identifier].all((id) => id.is_null)
}

class Identifier: FooNode implements TokenNode {
}

class Number: FooNode implements TokenNode {
}

class Sequence: FooNode {
    @parse_field items: ASTList[FooNode]

    @exported
    fun keep_list(): Array[Entity[Identifier]] = self.items.keep[Identifier]

    @exported
    fun keep_array(items: Array[Entity[FooNode]]): Array[Entity[Identifier]] =
        items.keep[Identifier]
}
