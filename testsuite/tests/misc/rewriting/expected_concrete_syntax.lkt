import lexer_example
grammar foo_grammar {
    @main_rule main_rule <- list*(def_rule)
    name <- Name(@identifier)
    def_rule <- Def(
        "def" name ?pick("(" list+(name, ",") ")") "=" expr
    )
    expr <- or(
        | Plus(expr "+" expr)
        | ParenExpr("(" expr ")")
        | Ref(name)
        | Literal(@number)
    )

}

@abstract class FooNode implements Node {
}

class Def : FooNode {
    @parse_field name : Name
    @parse_field args : ASTList[Name]
    @parse_field expr : Expr
}

@abstract class Expr : FooNode {
}

class Literal : Expr implements SymbolNode {
}

class ParenExpr : Expr {
    @parse_field expr : Expr
}

class Plus : Expr {
    @parse_field lhs : Expr
    @parse_field rhs : Expr
}

class Ref : Expr {
    @parse_field name : Name
}

class Name : FooNode implements SymbolNode {
}
