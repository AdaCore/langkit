@spacing(alphanumericals, alphanumericals)
lexer foo_lexer {

    whitespace <- p"[ \n\r\t]+"

    family alphanumericals {
        def <- "def"
    }

    var <- "var"

    family alphanumericals {
        error <- "error"
        example <- "example"
        null_tok <- "null"
    }

    comma <- ","
    dot <- "."
    semicolon <- ";"
    l_par <- "("
    r_par <- ")"
    l_brace <- "{"
    r_brace <- "}"
    equal <- "="
    plus <- "+"
    minus <- "-"
    less_than <- "<"

    family alphanumericals {
        number <- p"[0-9]+"
        @symbol identifier <- p"[a-zA-Z_][a-zA-Z0-9_]*"
    }

    string <- p"\"[^\"]*\""
    @newline_after comment <- p"#(.?)+"
}
grammar foo_grammar {
    @main_rule main_rule <- list+(pick(decl ";"))
    decl <- or(
        | VarDecl("def" name "=" expr)
        | FuncDecl(
            "def" name "(" list+(name, ",") ")" "=" expr
        )
    )
    expr <- or(Plus(atom "+" expr) | atom)
    atom <- or(Literal(@number) | name)
    name <- Name(@identifier)

}

@root_node class FooNode {

    @export fun identity (k : DeclKind): DeclKind = k
}

@abstract class Decl : FooNode {

    @export fun decl_kind (): DeclKind
}

class FuncDecl : Decl {
    @parse_field name : Name
    @parse_field args : ASTList[Name]
    @parse_field expr : Expression

    fun decl_kind (): DeclKind = DeclKind.func
}

class VarDecl : Decl {
    @parse_field name : Name
    @parse_field expr : Expression

    fun decl_kind (): DeclKind = DeclKind.var
}

@abstract class Expression : FooNode {
}

class Literal : Expression {
}

class Name : Expression {
}

class Plus : Expression {
    @parse_field left : Expression
    @parse_field right : Expression
}
