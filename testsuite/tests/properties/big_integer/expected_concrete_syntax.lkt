@spacing(alphanumericals, alphanumericals)
lexer foo_lexer {

    @text whitespace <- p"[ \n\r\t]+"

    family alphanumericals {
        @text def <- "def"
    }

    @text var <- "var"

    family alphanumericals {
        @text error <- "error"
        @text example <- "example"
        @text null_tok <- "null"
    }

    @text comma <- ","
    @text dot <- "."
    @text semicolon <- ";"
    @text l_par <- "("
    @text r_par <- ")"
    @text l_brace <- "{"
    @text r_brace <- "}"
    @text equal <- "="
    @text plus <- "+"
    @text minus <- "-"
    @text less_than <- "<"

    family alphanumericals {
        @text number <- p"[0-9]+"
        @symbol identifier <- p"[a-zA-Z_][a-zA-Z0-9_]*"
    }

    @text string <- p"\"[^\"]*\""
    @newline_after @text comment <- p"#(.?)+"
}
grammar foo_grammar {
    @main_rule main_rule <- ASTList[Decl]+(decl)
    decl <- Decl("def" name "=" expr)
    expr <- or(op | atom)
    op <- or(
        | Plus(atom "+" expr)
        | Minus(atom "-" expr)
        | Equal(atom "=" expr)
        | LessThan(atom "<" expr)
    )
    atom <- or(ref | literal)
    ref <- Ref(name)
    name <- Identifier(@identifier)
    literal <- Literal(@number)

}

@root_node class FooNode {

    @export fun identity (value : BigInt): BigInt = value
}

class Decl : FooNode {
    @parse_field name : Identifier
    @parse_field expr_tree : Expr
}

@abstract class Expr : FooNode {

    @export fun evaluate (): BigInt

    @export fun evaluate_as_int (): Int = node.evaluate.as_int
}

class Equal : Expr {
    @parse_field left : Expr
    @parse_field right : Expr

    fun evaluate (): BigInt =
    BigInt(if (node.left.evaluate = node.right.evaluate) then (1) else (0))
}

class LessThan : Expr {
    @parse_field left : Expr
    @parse_field right : Expr

    fun evaluate (): BigInt =
    BigInt(if (node.left.evaluate < node.right.evaluate) then (1) else (0))
}

class Literal : Expr {

    fun evaluate (): BigInt
}

class Minus : Expr {
    @parse_field left : Expr
    @parse_field right : Expr

    fun evaluate (): BigInt = node.left.evaluate - node.right.evaluate
}

class Plus : Expr {
    @parse_field left : Expr
    @parse_field right : Expr

    fun evaluate (): BigInt = node.left.evaluate + node.right.evaluate
}

class Ref : Expr {
    @parse_field name : Identifier

    fun evaluate (): BigInt =
    node.node_env.get(node.name.symbol, only_first=true).to[Decl].expr_tree.evaluate
}

class Identifier : FooNode {
}

struct EnvAssoc {
    key : SymbolType
    value : FooNode
    dest_env : LexicalEnv
    metadata : Metadata
}
