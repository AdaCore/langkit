# Test that property checks are properly emitted when null checks should trigger
# them.

import lexer_example

@with_lexer(foo_lexer)
grammar foo_grammar {
    @main_rule main_rule <- expression
    expression <- or(
        | pick("(" expression ")")
        | Plus(atom "+" main_rule)
        | atom
    )
    atom <- or(Literal(@Number) | Name(@Identifier))
}

@abstract
class FooNode implements Node[FooNode] {
    @exported
    fun null_unit(): AnalysisUnit = null[AnalysisUnit]

    @exported
    fun null_node(): Entity[Expression] = null[Entity[Expression]]

    @exported
    fun deref_null_unit(): Entity[FooNode] =
        node.null_unit().root.as_bare_entity

    @exported
    fun deref_null_node(): Entity[Expression] = node.null_node().null_node()

    @exported
    fun null_node_unit(): AnalysisUnit = node.null_node().unit

    @exported
    fun cast_null_node(): Entity[Name] = node.null_node().as[Name]

    @exported
    fun match_null_node(): Entity[Expression] = match node.null_node().node {
        case l: Literal => l
        case n: Name => n
        case others => others
    }.as_bare_entity
}

@abstract
class Expression: FooNode {
}

class Literal: Expression implements TokenNode {
}

class Name: Expression implements TokenNode {
    fun env_element(): Entity[FooNode] =
        node.children_env.get(node.symbol)?[0]

    @exported
    fun deref_env_element(): Entity[Expression] =
        node.env_element().null_node()

    @exported
    fun match_env_element(): Entity[FooNode] = match node.env_element() {
        case l: Entity[Literal] => l
        case n: Entity[Name] => n
        case others => others
    }
}

class Plus: Expression {
    @parse_field left: Expression
    @parse_field right: Expression
}
