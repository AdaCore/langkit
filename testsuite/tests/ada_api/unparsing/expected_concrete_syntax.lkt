import lexer_example

@with_lexer(foo_lexer)
grammar foo_grammar {
    @main_rule main_rule <- list*(or(var_decl | fun_decl | block | sync_rule))
    name <- Name(@identifier)
    type_ref <- name
    null_qual <- NullQual("null")
    var_decl <- VarDecl(
        null_qual "var" name ":" type_ref "=" expr ";"
    )
    param_spec <- ParamSpec(name ":" type_ref ?pick("=" expr))
    fun_decl <- FunDecl(
        "def"
        name
        pick("("
        list*(param_spec, ",")
        ")") ":" type_ref "{" list*(stmt) "}"
    )
    stmt <- or(
        | ExprStmt(expr ";")
        | AssignStmt(list+(name, ",") "=" expr ";")
    )
    expr <- or(
        | DottedName(expr "." name)
        | Addition(expr "+" expr)
        | call
        | atom
    )
    atom <- or(number | ref)
    number <- Number(@number)
    ref <- Ref(name)
    call_arg <- CallArg(?pick(name "=") expr)
    call <- Call(name "(" list*(call_arg, ",") ")")
    block <- Block("{" list*(or(var_decl | fun_decl)) "}")
    sync_rule <- SyncNode(
        "kwA" name "kwC" ?pick("kwD" name "kwE") "kwF"
    )
}

@abstract class FooNode implements Node[FooNode] {
}

class Block: FooNode {
    @parse_field items: ASTList[FooNode, Decl]
}

class CallArg: FooNode {
    @parse_field @nullable name: Name
    @parse_field expr: Expr
}

@abstract class Decl: FooNode {
    @abstract @parse_field name: Name
}

class FunDecl: Decl {
    @parse_field name: Name
    @parse_field args: ASTList[FooNode, ParamSpec]
    @parse_field return_type: Name
    @parse_field body: ASTList[FooNode, Stmt]
}

class VarDecl: Decl {
    @parse_field is_null: NullQual
    @parse_field name: Name
    @parse_field type_expr: Name
    @parse_field value: Expr
}

@abstract class Expr: FooNode {
}

class Addition: Expr {
    @parse_field lhs: Expr
    @parse_field rhs: Expr
}

class Call: Expr {
    @parse_field callee: Name
    @parse_field args: ASTList[FooNode, CallArg]
}

class DottedName: Expr {
    @parse_field prefix: Expr
    @parse_field suffix: Name
}

class Number: Expr implements TokenNode {
}

class Ref: Expr {
    @parse_field name: Name
}

class Name: FooNode implements TokenNode {
}

@qualifier enum class NullQual: FooNode {
}

class ParamSpec: FooNode {
    @parse_field name: Name
    @parse_field type_expr: Name
    @parse_field @nullable default_expr: Expr
}

@abstract class Stmt: FooNode {
}

class AssignStmt: Stmt {
    @parse_field names: ASTList[FooNode, Name]
    @parse_field expr: Expr
}

class ExprStmt: Stmt {
    @parse_field expr: Expr
}

|" Node used to test the token synchronization during unparsing: there are
|" both pre (kwA) and post (kwF) tokens for the node itself (SyncNode), an
|" inter token between the two fields (kwC) and pre/post tokens (kwD and kwE)
|" for child_2.
class SyncNode: FooNode {
    @parse_field child_1: Name
    @parse_field @nullable child_2: Name
}
