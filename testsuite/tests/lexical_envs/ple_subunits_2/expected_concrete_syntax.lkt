@spacing(alphanumericals, alphanumericals)
lexer foo_lexer {

    whitespace <- p"[ \n\r\t]+"

    family alphanumericals {
        def <- "def"
    }

    var <- "var"

    family alphanumericals {
        error <- "error"
        example <- "example"
        null_tok <- "null"
    }

    comma <- ","
    dot <- "."
    semicolon <- ";"
    l_par <- "("
    r_par <- ")"
    l_brace <- "{"
    r_brace <- "}"
    equal <- "="
    plus <- "+"
    minus <- "-"
    less_than <- "<"

    family alphanumericals {
        number <- p"[0-9]+"
        @symbol identifier <- p"[a-zA-Z_][a-zA-Z0-9_]*"
    }

    string <- p"\"[^\"]*\""
    @newline_after comment <- p"#(.?)+"
}
grammar foo_grammar {
    @main_rule main_rule <- list+(scope)
    deps <- list*(pick(Dep(pick("+" name)) ";"))
    defs <- list*(pick(or(scope | var) ";"))
    scope <- Scope(name "{" deps defs "}")
    var <- Var(id "=" name)
    id <- Id(@identifier)
    name <- or(Prefix(name "." id) | id)

}

@root_node class FooNode {
}

class Dep : FooNode {
    @parse_field name : Name
}

@abstract class Name : FooNode {

    @export fun resolve (): FooNode

    @export fun suffix_symbol (): SymbolType

    @export fun symbols (): Array[SymbolType]

    fun referenced_unit_or_error (or_error : Bool): InternalUnit

    @export fun referenced_unit (): InternalUnit =
    node.referenced_unit_or_error(false)
}

class Id : Name {

    fun resolve (): FooNode = node.node_env.get(node, only_first=true)

    fun suffix_symbol (): SymbolType = node.symbol

    fun symbols (): Array[SymbolType] = node.symbol?.singleton
}

class Prefix : Name {
    @parse_field prefix : Name
    @parse_field suffix : Id

    fun resolve (): FooNode =
    node.prefix.resolve.children_env.get(node.suffix.symbol, only_first=true)

    fun suffix_symbol (): SymbolType = node.suffix.symbol

    fun symbols (): Array[SymbolType] =
    node.prefix.symbols & node.suffix.symbols
}

class Scope : FooNode {
    @parse_field name : Name
    @parse_field deps : ASTList[Dep]
    @parse_field defs : ASTList[FooNode]

    fun initial_env (): LexicalEnv = match node.name {
        case p : Prefix => p.prefix.referenced_unit.root.to[ASTList[Scope]]!.filter(
            (scope) => scope.name.symbols = p.prefix.symbols
        )(0).children_env
        case _ => node.children_env
    }
}

class Var : FooNode {
    @parse_field name : Id
    @parse_field value : Name
}

struct EnvAssoc {
    key : SymbolType
    value : FooNode
    dest_env : LexicalEnv
    metadata : Metadata
}
