# Check that Populate_Lexical_Env is automatically called in public property
# dispatchers.

import lexer_example

@with_lexer(foo_lexer)
grammar foo_grammar {
    @main_rule main_rule <- defs
    defs <- list*(def_rule)
    def_rule <- or(scope | var)
    scope <- Scope(Id(@Identifier) "{" defs "}")
    var <- Var(Id(@Identifier) "=" name)
    name <- or(
        | Prefix(name "." Id(@Identifier))
        | Id(@Identifier)
    )
}

@abstract
class FooNode implements Node[FooNode] {
}

@abstract
class DefNode: FooNode {
}

class Scope: DefNode {
    @parse_field name: Id
    @parse_field defs: ASTList[DefNode]

    env_spec {
        add_to_env_kv(node.name.symbol, node)
        add_env()
    }
}

class Var: DefNode {
    @parse_field name: Id
    @parse_field value: Name

    env_spec {
        add_to_env_kv(node.name.symbol, node)
    }
}

@abstract
class Name: FooNode {
    @exported
    @abstract
    fun resolve(): Entity[FooNode]
}

class Id: Name implements TokenNode {
    fun resolve(): Entity[FooNode] = node.node_env.get_first(node.symbol)
}

class Prefix: Name {
    @parse_field prefix: Name
    @parse_field suffix: Id

    fun resolve(): Entity[FooNode] =
        node.prefix.resolve().children_env.get_first(node.suffix.symbol)
}
