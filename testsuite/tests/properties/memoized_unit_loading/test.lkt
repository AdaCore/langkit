# Check that property memoization works as expected whhen memoization table
# invalidation happens in the middle of their execution. In particular, check
# that the result of memoized properties are consistent, and would be the same
# as if properties were not memoized.

import lexer_example

@with_lexer(foo_lexer)
grammar foo_grammar {
    @main_rule main_rule <- Example("example")
}

@abstract
class FooNode implements Node[FooNode] {
}

class Example: FooNode {

    # External properties

    @external()
    fun ext_fetch_example_unit(): AnalysisUnit

    @external()
    fun ext_unit_count(): Int

    # External property wrappers (for tracing)

    @memoized
    @traced
    fun fetch_example_unit(): AnalysisUnit = node.ext_fetch_example_unit()

    @memoized
    @traced
    fun unit_count(): Int = node.ext_unit_count()

    # Test entry point

    @exported
    @memoized
    @traced
    fun mmz_prop(): Array[Int] = {
        # Both calls to unit_count are memoized, but when called for the first
        # time, the first one's result will be different from the second one.
        val before = [node.unit_count()];
        # Update context version by parsing a new unit
        val _ = node.fetch_example_unit();
        val after = [node.unit_count()];

        before & after
    }
}
