# Check that set_initial_env actions that fetch a foreign environment without
# the unsafe=True flag raise an error.

from lexer_example import foo_lexer

@with_lexer(foo_lexer)
grammar foo_grammar {
    @main_rule main_rule <- list+(scope)
    scope <- Scope(identifier "{" list*(scope) "}")
    identifier <- or(
        | ScopedId(identifier "." simple_identifier)
        | simple_identifier
    )
    simple_identifier <- SimpleId(@Identifier)
}

@abstract
class FooNode implements Node[FooNode] {
}

@abstract
class Id: FooNode {
    @abstract
    fun designated_symbol(): Symbol

    @abstract
    fun designated_scope(base_env: LexicalEnv): LexicalEnv

    @abstract
    fun resolve(base_env: LexicalEnv): FooNode
}

class ScopedId: Id {
    @parse_field scope: Id
    @parse_field name: SimpleId

    fun designated_symbol(): Symbol = node.name.symbol

    fun designated_scope(base_env: LexicalEnv): LexicalEnv =
        node.scope.resolve(base_env).children_env

    fun resolve(base_env: LexicalEnv): FooNode =
        node.designated_scope(base_env).get_first(node.name.symbol).node
}

class SimpleId: Id implements TokenNode {
    fun designated_symbol(): Symbol = node.symbol

    fun designated_scope(base_env: LexicalEnv): LexicalEnv = base_env

    fun resolve(base_env: LexicalEnv): FooNode =
        base_env.get_first(node.symbol).node
}

class Scope: FooNode {
    @parse_field name: Id
    @parse_field content: ASTList[Scope]

    env_spec {
        set_initial_env(
            DesignatedEnv(
                kind=DesignatedEnvKind.direct_env,
                env_name=null[Symbol],
                direct_env=node.name.designated_scope(node.parent.children_env)
            )
        )
        add_to_env_kv(node.name.designated_symbol(), node)
        add_env()
    }
}

@metadata
struct Metadata {
    @used_in_equality node: FooNode
}
